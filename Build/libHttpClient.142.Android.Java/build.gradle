apply plugin: 'java' 
apply plugin: 'maven-publish' 

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript
{
    repositories
    {
       jcenter()
       mavenCentral()
    }

    dependencies
    {
        classpath 'com.android.tools.build:gradle-experimental:0.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects
{
    repositories
    {
        jcenter()
        mavenCentral()
    }
}

// To publish artifacts locally, first build the .aar with visual studio and then run the following command from /Build/libHttpClient.141.Android.Java/
// gradlew.bat publish -PHCVersion=[Version] -PLocalMavenRepositoryPath=[Path to local maven repository]

publishing
{
    publications
    {
        mavenAar(MavenPublication)
        {
            groupId 'libHttpClientAndroid'
            artifactId 'libHttpClient'
            version (project.hasProperty("HCVersion") ? HCVersion : '0.0.0')
            artifact (project.hasProperty('InputAar') ? InputAar : '../../Binaries/Release/x86/libHttpClient.141.Android.Java/libHttpClient.aar')

            pom.withXml 
            {
                def dependencies = asNode().appendNode('dependencies')
                project(':app').configurations.compile.allDependencies.each 
                {
                    if (it.group != null)
                    {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        } 
    } 

    repositories
    {
        if (project.hasProperty("PublishRemote") && (System.getenv("VSTS_ENV_ACCESS_TOKEN") != null || project.hasProperty("vstsMavenAccessToken")))
        {
            maven
            {
                url 'https://microsoft.pkgs.visualstudio.com/_packaging/gaming/maven/v1'
                credentials 
                {
                    username "VSTS" 
                    //The VSTS build system will use the "VSTS_ENV_ACCCESS_TOKEN" to authenticate to VSTS feeds, otherwise it will fall back on the "vstsMavenAccessToken" property, 
                    // and as a final fallback it will pass an invalid token so other build tasks can continue to succeed.
                    password System.getenv("VSTS_ENV_ACCESS_TOKEN") != null ? System.getenv("VSTS_ENV_ACCESS_TOKEN") : vstsMavenAccessToken
                } 
            }
        }
        else if (System.getenv("TFS_DropLocation") != null)
        {
            maven
            {
                url ('file:///' + System.getenv("TFS_DropLocation") + '/Maven')
            }
        }
        else
        {
            maven
            {
                url (project.hasProperty("LocalMavenRepositoryPath") ? 'file:///' + LocalMavenRepositoryPath : "file://${buildDir}/Maven")
            }
        }
    }
}
